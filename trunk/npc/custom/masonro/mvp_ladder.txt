//	....... if your server has { Overwriting user function [int__] } don't blame me ..........
function	script	int__	{
	set .@num, atoi(getarg(0));
	if ( .@num == 0 || .@num >= 2147483647 ) return getarg(0);
	set .tmpNum$, .@num;
	set .@l, getstrlen(.tmpNum$);
	for ( set .@i,0; .@i < .@l; set .@i, .@i + 1 ) {
		set .@num$, .@num % pow(10,.@i+1) / pow(10,.@i) + .@num$;
		if ( (.@i+1) % 3 == 0 && .@i+1 != .@l ) set .@num$, ","+ .@num$;
	}
	return .@num$;
}

//===== eAthena Script ======================================
//= MvP Ladder Game Script
//===== By: ===============================================
//= by aftermath (1.0)
//= by ~AnnieRuru~ (2.0)
//===== Current Version: ======================================
//= 2.5
//===== Compatible With: ===================================== 
//= eAthena 13405 Trunk
//===== Description: ========================================= 
//= This script warps you to an arena where you have to kill all MvP bosses in
//= accending or more like from weakest to strongest. 
//===== Topic =================================================
//= http://www.eathena.ws/board/index.php?showtopic=178049
//===== Additional Comments: ================================== 
//= 1.0 & 1.1 by aftermath
//= 2.0 ~by AnnieRuru~ rewrite everything with new MVPs
//= 2.1
//= ---- now announce rounds
//= ---- fix the misleading .totalround variable
//= ---- just noticed the original script enabled noteleport mapflag, so I uncommented it
//= ---- optimize the script a bit
//= ---- the map name is store in a variable now, easier to change the map location
//= ---- fix a bug if 2nd player manage to come in the map to ks the 1st player, (eg : guild recall skill for example ) the game will still continue on and Zeny still reward to the mvp killer
//= ---- fix a bug if the player manage to kill all mvps and didn't turn on @autoloot, he will not collect the loot from final mvp
//= ---- eliminate the error show on map-server if the player disconnect while loading the mvp ladder map
//= 2.2
//= ---- add item reward on complete each round and final round ( idea thx to Qasther )
//= ---- added noloot mapflag, commented because some of you like it
//= ---- added config delay time
//= ---- added config on displaying announcements
//= 2.2a
//= ---- TODO is done. now whoever wants to change npc location just have to change the npc header. script handle the rest
//= 2.3
//= ---- move the config to the top of the file, easier to config I think
//= ---- Zeny reward no longer use for-loop, because I noticed nobody know how to configure Zeny reward properly
//= ---- eliminate all global variables ... into npc variables. I prefer npc variables
//= ---- added entrance fee configuration, can choose between item cost or Zeny cost
//= ---- added waiting room feature to show the count down timer since the player went in
//= ---- added time attack feature to prevent player @at'ing inside the map because 1st round is a passive monster
//= ---- added best record to encourage your players beat the best score
//= ---- added 2 more checks to prevent 2 players inside the map, except GMs - loadevent with 20 seconds delay
//= ---- oh and, move the map from guild_vs2 to mvp_ladder
//= 2.3a
//= ---- added GM level configuration to reset the best record
//= ---- change the misleading "time left" announcement
//= ---- added prize reward if beaten the best record
//= ---- fix a typo that nobody can beat the best record ...
//= 2.4
//= ---- added deny usage ( suggested by llama123 )
//= ---- added GM can reset other players deny usage
//= ---- change a bit the way it announce minutes and seconds
//= ---- FIX a CRITICAL TYPO the script doesn't deduct Zeny after register ( wah nobody report this )
//= 2.4a
//= ---- added an announcement when the player failed to finish the ladder
//= ---- gm menu don't have a reset deny option if in the config doesn't has a value set
//= 2.5
//= ---- int__ function into main script
//= ---- GM not suppose to beat player's best record ...
//= ---- rewrite waitingroom system
//= ---- fix a minor bug if the player already warp out within 10 seconds, now no longer warp again
//= ---- added a feature if the player completed within a set amount of time, a bonus item is given
//= ---- player will receive a message if the deny usage is reset by a GM
//= ---- if account deny usage is equal or more than player's deny usage, player deny usage is deleted to save space
//========================================================

// Configure Mapflags ------------------------
mvp_ladder	mapflag	nobranch
mvp_ladder	mapflag	nomemo
mvp_ladder	mapflag	nopenalty	// disable exp loss
mvp_ladder	mapflag	noreturn
mvp_ladder	mapflag	nosave	SavePoint
mvp_ladder	mapflag	noteleport
mvp_ladder	mapflag	nowarp
mvp_ladder	mapflag	noexp
mvp_ladder	mapflag	nowarpto
mvp_ladder	mapflag	nomobloot	// disable monster drop loots,
mvp_ladder	mapflag	nomvploot	// 2 of these
mvp_ladder	mapflag	loadevent
mvp_ladder	mapflag	nocommand	60
mvp_ladder	mapflag	partylock

mvp_ladder,0,0,0	script	ev_Mvp	-1,{
OnInit:
//	Configurations -----------------------------------------------------

//	set entrance fee. Note: Only choose Zeny OR item. If item amount if specify, the script will use item
	set .entryitemid, 674; // 674 - mithril coin
	set .entryitemamount, 0; // by default is using Zeny
	set .entryZeny, 100000;

//	id of each mvp. you can add more
	setarray .mvpid[1],
		1252,//	Garm				73
		1418,//	Evil Snake Lord		73
		1251,//	Knight of Windstorm	77
		1658,//	Ygnizem				79
		1871,//	Fallen Bishop		80
		1785,//	Atroce				82
		1389,//	Dracula				85
		1630,//	Bacsojin			85
		1885,//	Gorynych			85
		1623,//	RSX 0806			86
		1511,//	Amon Ra				88
		1688,//	Lady Tanee			89
		1768,//	Gloom Under Night	89
		1719,//	Datale				90
		1734,//	Kiel D-01			90
		1779,//	Ktullanux			98
		1874,//	Beelzebub			98
		rand(1646,1651),//	Lord Knight Seyren 99 || Assassin Cross Eremes 99 || Whitesmith Harword 99 || High Priest Magaleta 99 || Sniper Shecil 99 || High Wizard Katrinn 99
		1751,//	Valkyrie Randgris	99
		1832;//	Ifrit				99

//	total rounds - Default has 39 rounds
	set .totalround, getarraysize(.mvpid) -1;

//	set the Zeny reward
	set .Reward[1], 100000;
	set .Reward[2], 200000;
	set .Reward[3], 300000;
	set .Reward[4], 400000;
	set .Reward[5], 500000;
	set .Reward[6], 600000;
	set .Reward[7], 700000;
	set .Reward[8], 800000;
	set .Reward[9], 900000;
	set .Reward[10], 1000000;
	set .Reward[11], 1100000;
	set .Reward[12], 1200000;
	set .Reward[13], 1300000;
	set .Reward[14], 1400000;
	set .Reward[15], 1500000;
	set .Reward[16], 1600000;
	set .Reward[17], 1700000;
	set .Reward[18], 1800000;
	set .Reward[19], 1900000;
	set .Reward[20], 2000000;

//	Time attack. The player has to beat all MVPs within # minutes. When the time is up, will warp to respawn point. Do Not set this as 0
	set .timeout, 30;

//	set item reward on completing each round. STACK with Zeny reward
	set .itemid, 616; // old card album
	set .itemamount, 1;

//	item reward on completion whole ladder
	set .lastitemid, 12246; // mystical card album
	set .lastitemamount, 10;

//	item reward when beaten the best record
	set .topitemid, 12246;
	set .topitemamount, 10;
	set .gmnotop, 20; // GM not suppose to beat player's record ...

//	If the player completed the ladder within this amount of time in minutes, they will get extra items for trying their best
	set .hightime, 15; // if complete within 45 minutes, the reward is given
	set .highitemid, 12246;
	set .highitemamount, 20;

//	time delay for next round, in seconds. Default is 3
	set .delay, 5;

//	set respawn point after finished the mvp ladder game
	set .respawnmap$, "SavePoint";
	set .respawnx, 0;
	set .respawny, 0;

//	set this to disallow player use the room for # MINUTES after registering, prevent same player go in again and again
	set .usagedeny_player, 60; // deny that character each time register
	set .usagedeny_account, 60; // deny whole account each time register

//	make announcement when player join MVP ladder
	set .joinann, 2; // 0 - disable announcement, 1- announce to server, 2- announce to town where mvp ladder warper located
//	make announcement when player finished MVP ladder
	set .quitann, 2; // 0 - disable announcement, 1- announce to server, 2- announce to town where mvp ladder warper located

//	minimum GM level in your server consider as Support GM, to allow these GMs get into this map
//	in the case player complain this script bug again ...
//	please tell them to use @jumpto or @warp
	set .mingmlvl, 20;

//	minimum gm level can reset the deny usage, to allow that player can play this game for 1 more time without waiting
//	this setting also allow the GM himself can play this ladder without the deny usage
	set .gmcandeny, 70;

//	minimum gm level can reset the best record.
	set .gmlvlreset, 99;

//	Config Ends --------------------------------------------------------------

	getmapxy .eventmap$, .@x, .@y, 1;
	if ( .joinann < 0 || .joinann > 2 ) set .joinann, 0;
	set .usagedeny_player, .usagedeny_player * 60;
	set .usagedeny_account, .usagedeny_account * 60;
	if ( .usagedeny_account >= .usagedeny_player ) set .usagedeny_player, 0;
	if ( .gmnotop == 0 ) set .gmnotop, 20;
	mapannounce .eventmap$, "Admin has refresh the server, please register again. Sorry for inconvenience.", 1;
	mapwarp .eventmap$, "eden_in01", 182, 204;
	
	end;

OnEvent:
	set .round, 0;
	killmonsterall .eventmap$;
	announce "You have "+ .timeout +" minutes to complete total "+ .totalround +" Rounds.", 1;
OnMvpDead:
	set .round, .round + 1;
	if ( .round >= 2 ) {
		if ( .Reward[.round-1] ) {
			set .tmpReward$, .Reward[.round-1];
			set .Reward$, callfunc("int__", .tmpReward$);
			message strcharinfo(0), "You got "+ .Reward$ +" reward Zeny.";
			set Zeny, Zeny + .Reward[.round-1];
		}
		if ( .itemamount && .round != .totalround +1 )
			getitem .itemid, .itemamount;
	}
	if ( .round == .totalround +1 ) {
		announce "Amazing ... You able to beat all the MVPs !", 1;	
		// bonus random MVP card
		//set @itemIDfromList, callfunc("F_RandMes",45,4147,4168,4142,4137,4123,4263,4276,4146,4132,4131,4143,4135,4144,4148,4121,4305,4236,4359,4425,4372,4145,4386,4134,4330,4441,4324,4408,4128,4363,4365,4430,4263,4403,4318,4419,4376,4357,4005,4367,4302,4399,4407,4374,4361,4352);
		//set @itemIDfromList, groupranditem(IG_Louise_Costume_Box);

		set @mvp_ladder_points, 10;
		set #MVP_LADDER_PTS,#MVP_LADDER_PTS+@mvp_ladder_points;
		dispbottom "You got "+@mvp_ladder_points+" MVP ladder points bonus. Current Ladder Points: "+#MVP_LADDER_PTS;
		
		switch(rand(7)) {
			case 0:		
			case 1: 
			case 2:
			case 3:
			case 4:
				set @itemIDfromList, groupranditem(IG_Cherish_Box);	
				if (@itemIDfromList == 18529) {
					announce strcharinfo(0) + " got "+getitemname(@itemIDfromList)+" from MvP Ladder game.", 0;
				}
				getitem @itemIDfromList,1;				
				dispbottom "Congratulations! You got '"+getitemname(@itemIDfromList)+"' from MvP Ladder game.";
				break;
			case 5:
				set @mvp_ladder_points, 10;
				set #MVP_LADDER_PTS,#MVP_LADDER_PTS+@mvp_ladder_points;
				dispbottom "Congratulations! You got "+@mvp_ladder_points+" MVP bonus ladder points reward from MvP Ladder game.";
				break;
			
			case 6:
				set @card_points_bonus, 500;
				set #CARD_TRADEIN_PTS,#CARD_TRADEIN_PTS+@card_points_bonus;
				dispbottom "Congratulations! You got "+@card_points_bonus+" card points reward from MvP Ladder game.";
				break;				
		}
		
		set .@timeused, .timeout * 60 - .remaintime;
		if ( .highitemamount && .@timeused < .hightime * 60 ) {
			announce "A bonus item has rewarded for completion within "+ .hightime +" minutes.", 1;
			getitem .highitemid, .highitemamount;
		}
		if ( ( $mvplads_time == 0 || .@timeused < $mvplads_time ) && getgmlevel() < .gmnotop ) {
			announce "And you beat the RECORD !!! [ "+( .@timeused / 60 )+" min "+( .@timeused % 60 )+" sec ]", 1;
			getitem .topitemid, .topitemamount;
			set $mvplads_name$, strcharinfo(0);
			set $mvplads_time, .@timeused;
		}
		else
			announce "Time used [ "+( .@timeused / 60 )+" min "+( .@timeused % 60 )+" sec ]", 1;
		sleep2 10000;
				
		set getvariableofnpc(.remaintime,"ev_Mvp"), 0;
		delwaitingroom strnpcinfo(0);
		
		if ( !playerattached() ) end;
		getmapxy .@map$, .@x, .@y, 0;
		if ( .@map$ == .eventmap$ )
			warp .respawnmap$, .respawnx, .respawny;
		if ( .quitann == 1 )
			announce strcharinfo(0) +" has finished the MvP ladder !", 0;
		else if ( .quitann == 2 )
			mapannounce .npcmap$, strcharinfo(0) +" has finished the MvP ladder !", 0;
		end;
	}
	else if ( .round == .totalround )
		announce "Final Round will begin in "+ .delay +" sec.", 1;
	else
		announce "Round "+ .round +" will begin in "+ .delay +" sec.", 1;
	sleep .delay * 1000;
	if ( .mvpid[.round] >= 1646 && .mvpid[.round] <= 1651 )
		monster .eventmap$,0,0,"--ja--",rand(1646,1651),1,"ev_Mvp::OnMvpDead";
	else
		monster .eventmap$,0,0,"--ja--",.mvpid[.round],1,"ev_Mvp::OnMvpDead";
	end;
}

eden_in01,190,208,4	script	MvP Ladder	413,{
	mes "[^FF0000MvP Ladder Warper^000000]";
	mes "Welcome to MvP ladder game.";
	mes "In this game, you have to kill every single MvP monster in accending order, starting from weakest to the strongest.";
	mes "Everytime you beaten a MVP, you will reward with Zeny.";
	if ( getvariableofnpc(.lastitemamount,"ev_Mvp") )
		mes "If you can finish the MVP ladder, you will earn "+ getvariableofnpc(.lastitemamount,"ev_Mvp") +" "+ getitemname(getvariableofnpc(.lastitemid,"ev_Mvp")) +".";
	if ( getvariableofnpc(.entryitemamount,"ev_Mvp") )
		mes "But the entrance fee is "+ getvariableofnpc(.entryitemamount,"ev_Mvp") +" "+ getitemname(getvariableofnpc(.entryitemid,"ev_Mvp")) +".";
	else if ( getvariableofnpc(.entryZeny,"ev_Mvp") )
		set .tmpEntry$, getvariableofnpc(.entryZeny,"ev_Mvp");
		set .entryzen$, callfunc("int__", .tmpEntry$ );
		mes "But the entrance fee is "+ .entryzen$ +" Zeny";
	next;
	mes "[^FF0000MvP Ladder Warper^000000]";
	mes "So... do you want to play the game?";
	next;
	if ( select("Yes, lets get it on.","Show me the best record.","No.",( getgmlevel() >= getvariableofnpc(.gmcandeny,"ev_Mvp") && ( getvariableofnpc(.usagedeny_player,"ev_Mvp") || getvariableofnpc(.usagedeny_account,"ev_Mvp") ) )?"Reset a player deny usage":"") == 3 ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "When you are strong enough to complete the game, please come back.";
		close;
	}
	else if ( @menu == 2 ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		if ( $mvplads_time == 0 ) {
			mes "Nobody finish this game before.";
			close;
		}
		else {
			mes "The best record is";
			mes "[ "+( $mvplads_time / 60 )+" min "+( $mvplads_time % 60 )+" sec ]";
			mes "By the player ^0000FF"+ $mvplads_name$ +"^000000.";
			if ( getgmlevel() < getvariableofnpc(.gmlvlreset,"ev_Mvp") ) close;
			next;
			if ( select("Close.", "Reset it.") == 1 ) close;
			if ( select("Nevermind.", "I really want to reset it.") == 1 ) close;
			mes "[^FF0000MvP Ladder Warper^000000]";
			mes "Record reset successfully.";
			set $mvplads_name$, "";
			set $mvplads_time, 0;
			close;
		}
	}
	else if ( @menu == 4 ) {
		if ( getgmlevel() < getvariableofnpc(.gmcandeny,"ev_Mvp") ) end;
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "Enter the player name";
		next;
		input .@name$;
		set .@aid, getcharid(3, .@name$);
		mes "[^FF0000MvP Ladder Warper^000000]";
		if ( .@aid == 0 ) {
			mes "That player is not online or not exist.";
			close;
		}
		set .@origin, getcharid(3);
		attachrid .@aid;
		set mvplads_deny, 0;
		set #mvplads_deny, 0;
		message strcharinfo(0), "GM has allow you to play MVP Ladder right now.";
		attachrid .@origin;
		mes rid2name(.@aid) +" can play this game now.";
		close;
	}
	if ( gettimetick(2) < mvplads_deny + getvariableofnpc(.usagedeny_player,"ev_Mvp") ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "I'm sorry, please wait for another";
		set .@left, mvplads_deny + getvariableofnpc(.usagedeny_player,"ev_Mvp") - gettimetick(2);
		set .@day,  .@left / (24*60*60);
		set .@hour, .@left % (24*60*60) / (60*60);
		set .@min,  .@left % (24*60*60) % (60*60) / (60);
		set .@sec,  .@left % (24*60*60) % (60*60) % (60);
		mes "^FF0000"+( (.@day)?(.@day +" day "):"" )+( (.@hour||.@day)?(.@hour +" hour "):"" )+( (.@min||.@hour||.@day)?(.@min +" min "):"")+ .@sec +" sec^000000";
		mes "This will allow others to take their turns. Please come back later.";
		close;
	}
	else if ( gettimetick(2) < #mvplads_deny + getvariableofnpc(.usagedeny_account,"ev_Mvp") ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "I'm sorry, please wait for another";
		set .@left, #mvplads_deny + getvariableofnpc(.usagedeny_account,"ev_Mvp") - gettimetick(2);
		set .@day,  .@left / (24*60*60);
		set .@hour, .@left % (24*60*60) / (60*60);
		set .@min,  .@left % (24*60*60) % (60*60) / (60);
		set .@sec,  .@left % (24*60*60) % (60*60) % (60);
		mes ( (.@day)?(.@day +" day "):"" )+( (.@hour||.@day)?(.@hour +" hour "):"" )+( (.@min||.@hour||.@day)?(.@min +" min "):"")+ .@sec +" sec.";
		mes "for your account to play again.";
		close;
	}
	else if ( countitem( getvariableofnpc(.entryitemid,"ev_Mvp") ) < getvariableofnpc(.entryitemamount,"ev_Mvp") ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "You don't have enough "+ getitemname(getvariableofnpc(.entryitemid,"ev_Mvp")) +", please come back when you do.";
		close;
	}
	else if ( getvariableofnpc(.entryitemamount,"ev_Mvp") == 0 && Zeny < getvariableofnpc(.entryZeny,"ev_Mvp") ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "You don't have enough Zeny, please come back when you do.";
		close;
	}
	else if ( .inuse || getmapusers( getvariableofnpc(.eventmap$,"ev_Mvp") ) >= 1 ) {
		mes "[^FF0000MvP Ladder Warper^000000]";
		mes "I'm sorry but a player is currently playing the game. Please standby until the player finishes the game or gets killed.";
		mes "Thank You.";
		close;
	}
	if ( getvariableofnpc(.joinann,"ev_Mvp") )
		announce strcharinfo(0) +" has started the MvP ladder game.",(getvariableofnpc(.joinann,"ev_Mvp") -1);
	if ( getgmlevel() < getvariableofnpc(.gmcandeny,"ev_Mvp") && getvariableofnpc(.usagedeny_player,"ev_Mvp") )
		set mvplads_deny, gettimetick(2);
	if ( getgmlevel() < getvariableofnpc(.gmcandeny,"ev_Mvp") && getvariableofnpc(.usagedeny_account,"ev_Mvp") )
		set #mvplads_deny, gettimetick(2);
	percentheal 100,100;
	warp getvariableofnpc( .eventmap$, "ev_Mvp" ), 50, 49;
	set .inuse, 1;
	set .inuseid, getcharid(3);
	set .inusename$, strcharinfo(0);
	if ( getvariableofnpc(.entryitemamount,"ev_Mvp") )
		delitem getvariableofnpc(.entryitemid,"ev_Mvp"), getvariableofnpc(.entryitemamount,"ev_Mvp");
	else 
		set Zeny, Zeny - getvariableofnpc(.entryZeny,"ev_Mvp");
	initnpctimer;
	doevent "ev_Mvp::OnEvent";
	end;

OnTimer20:
	set getvariableofnpc(.starttime,"ev_Mvp"), gettimetick(2);
	do {
		set getvariableofnpc(.remaintime,"ev_Mvp"), getvariableofnpc(.timeout,"ev_Mvp") * 60 + getvariableofnpc(.starttime,"ev_Mvp") - gettimetick(2);
		set .@hour, getvariableofnpc(.remaintime,"ev_Mvp") / 3600 ;
		set .@min, getvariableofnpc(.remaintime,"ev_Mvp") % 3600 / 60 ;
		set .@sec, getvariableofnpc(.remaintime,"ev_Mvp") % 3600 % 60 ;
		delwaitingroom strnpcinfo(0);
		waitingroom .inusename$ + " "+( ( .@hour )?( .@hour +":"):"" )+( ( .@min < 10 )?"0"+ .@min: .@min )+":"+( ( .@sec < 10 )?"0"+ .@sec: .@sec ), 0;
		if ( getmapusers( getvariableofnpc(.eventmap$,"ev_Mvp") ) == 0 && .inuse == 0 ) break;
		else if ( .@sec ) {
			switch ( .@min ) {
				case 30:
				case 20:
				case 10:
				case 5:
				case 3:
				case 1:
					mapannounce getvariableofnpc(.eventmap$,"ev_Mvp"), "You have "+ .@min +":"+ ( ( .@sec < 10 )?"0"+ .@sec: .@sec )+" time left to complete MVP ladder game.",bc_blue;
					//dispbottom "You have "+ .@min +" min left to complete MVP ladder game.";
				default:
			}
		}
		sleep 995;
	} while ( getvariableofnpc(.remaintime,"ev_Mvp") > 1 );
	if ( getvariableofnpc(.round,"ev_Mvp") != getvariableofnpc(.totalround,"ev_Mvp") +1 ) {
		killmonsterall getvariableofnpc(.eventmap$,"ev_Mvp");
		mapannounce getvariableofnpc(.eventmap$,"ev_Mvp"), "You failed to kill all MVPs in time.", 0;
		if ( getvariableofnpc(.quitann,"ev_Mvp") == 1 )
			announce .inusename$ +" failed to finish the MvP ladder !", 0;
		else if ( getvariableofnpc(.quitann,"ev_Mvp") == 2 )
			mapannounce getvariableofnpc(.npcmap$,"ev_Mvp"), .inusename$ +" failed to finish the MvP ladder !", 0;
		if ( attachrid(.inuseid) ) {
			getmapxy .@map$, .@x, .@y, 0;
			if ( .@map$ == getvariableofnpc(.eventmap$,"ev_Mvp") )
				warp getvariableofnpc(.respawnmap$,"ev_Mvp"), getvariableofnpc(.respawnx,"ev_Mvp"), getvariableofnpc(.respawny,"ev_Mvp");
		}
	}
	set getvariableofnpc(.remaintime,"ev_Mvp"), 0;
	delwaitingroom strnpcinfo(0);
	end;

OnTimer20000:
	set .inuse, 0;
	end;

OnInit:
	sleep 1;
	getmapxy .@map$, .@x, .@y, 1;
	set getvariableofnpc(.npcmap$,"ev_Mvp"), .@map$;
	end;

OnPCLoadMapEvent:
	sleep2 1;
	getmapxy .@map$, .@x, .@y, 0;
	if ( .@map$ != getvariableofnpc(.eventmap$,"ev_Mvp") ) end;
	if ( getcharid(3) == .inuseid || getgmlevel() >= getvariableofnpc(.mingmlvl,"ev_Mvp") ) end;
	warp getvariableofnpc(.respawnmap$,"ev_Mvp"), getvariableofnpc(.respawnx,"ev_Mvp"), getvariableofnpc(.respawny,"ev_Mvp");
	announce "You are forbidden to enter MVP ladder game without registration.", 3, 0xFF0000;
	end;
}

mvp_ladder,57,59,0	script	Healer Angeling#Mvp	1096,2,2,{

startAgain:
OnInit:
    initnpctimer;
    end;
OnTimer5000: //15 seconds
     movenpc "Healer Angeling#Mvp",42,59; 	 
     end;
OnTimer10000: //15 seconds
     movenpc "Healer Angeling#Mvp",42,39; 	 
     end;
OnTimer15000: //30 seconds
     movenpc "Healer Angeling#Mvp",57,39; 	 
     end;
OnTimer20000: //30 seconds
     movenpc "Healer Angeling#Mvp",57,59; 
	 goto startAgain;
     end;

OnTouch:
    if (Hp == MaxHp && Sp == MaxSp) {
    end;
    }
    npctalk "Heal !!";
    skilleffect 28,9999;
    percentheal 100,100;
	atcommand "#item " + strcharinfo(0) + " 7139" ; // Glistering Coat for FCP
    sc_start SC_ANGELUS,600000,10; // Angelus lvl 10
    sc_start SC_BLESSING,600000,10; // Blessing lvl 10
    sc_start SC_INCREASEAGI,600000,10; // Increase agi lvl 10
    sc_start SC_IMPOSITIO,600000,5; // Impositio manus lvl 5
    sc_start SC_SUFFRAGIUM,600000,3; // Suffragium lvl 3
    sc_start SC_MAGNIFICAT,600000,5; // Magnificat lvl 10
    sc_start SC_WINDWALK,600000,10; // Wind walk lvl 10
    sc_start SC_NEN,600000,5; // Soul ninja skill lvl 5
    sc_start SC_CONCENTRATION,600000,10; // Improve Concentration lvl 10
    atcommand "@useskill 479 5 " +strcharinfo(0); // Full chemical protection lvl 5
    end;
end;
}